apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.simplemobiletools.smsmessenger"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 72
        versionName "5.17.4"
        setProperty("archivesBaseName", "sms-messenger")
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "variants"
    productFlavors {
        core {}
        fdroid {}
        prepaid {}
        traccar {
            applicationId 'org.traccar.gateway'
            versionCode 8
            versionName '5.0'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation 'com.github.SimpleMobileTools:Simple-Commons:52fd420d35'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'com.github.tibbi:IndicatorFastScroll:4524cd0b61'
    implementation 'com.github.tibbi:android-smsmms:33fcaf94d9'
    implementation "me.leolin:ShortcutBadger:1.1.22"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.11.3'
    implementation 'androidx.lifecycle:lifecycle-process:2.5.1'

    kapt "androidx.room:room-compiler:2.4.3"
    implementation "androidx.room:room-runtime:2.4.3"
    annotationProcessor "androidx.room:room-compiler:2.4.3"

    traccarImplementation 'org.eclipse.jetty:jetty-server:8.1.21.v20160908'
    traccarImplementation platform('com.google.firebase:firebase-bom:29.0.3')
    traccarImplementation 'com.google.firebase:firebase-core'
    traccarImplementation 'com.google.firebase:firebase-analytics'
    traccarImplementation 'com.google.firebase:firebase-crashlytics'
    traccarImplementation 'com.google.firebase:firebase-messaging'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains('Traccar')) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'

    task copyJson(type: Copy) {
        from '../../environment/google-services'
        into '.'
        include 'traccar-sms-gateway.json'
        rename('traccar-sms-gateway.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}
